context JUDOPSM!DurationUnit {

    // rate of second unit to smaller temporal unit(s) should be valid
    constraint SecondRateIsValid {
        guard: self.unitType = JUDOPSM!DurationType#second
        
        check: self.getRates(JUDOPSM!DurationType#millisecond).select(r | r.get('dividend') <> r.get('divisor') * 1000).size() == 0
        message: "Rate of second (" + self.name + ") temporal measure unit is invalid: " + self.getRates(JUDOPSM!DurationType#millisecond)
    }
    
    // rate of minute unit to smaller temporal unit(s) should be valid
    constraint MinuteRateIsValid {
        guard: self.unitType = JUDOPSM!DurationType#minute
        
        check: self.getRates(JUDOPSM!DurationType#millisecond).select(r | r.get('dividend') <> r.get('divisor') * 60000).size() == 0
           and self.getRates(JUDOPSM!DurationType#second).select(r | r.get('dividend') <> r.get('divisor') * 60).size() == 0
        message: "Rate of minute (" + self.name + ") temporal measure unit is invalid: " +
                 self.getRates(JUDOPSM!DurationType#millisecond)
                     .includingAll(self.getRates(JUDOPSM!DurationType#second))
    }
    
    // rate of hour unit to smaller temporal unit(s) should be valid
    constraint HourRateIsValid {
        guard: self.unitType = JUDOPSM!DurationType#hour
        
        check: self.getRates(JUDOPSM!DurationType#millisecond).select(r | r.get('dividend') <> r.get('divisor') * 3600000).size() == 0
           and self.getRates(JUDOPSM!DurationType#second).select(r | r.get('dividend') <> r.get('divisor') * 3600).size() == 0
           and self.getRates(JUDOPSM!DurationType#minute).select(r | r.get('dividend') <> r.get('divisor') * 60).size() == 0
        message: "Rate of hour (" + self.name + ") temporal measure unit is invalid: " + 
                 self.getRates(JUDOPSM!DurationType#millisecond)
                     .includingAll(self.getRates(JUDOPSM!DurationType#second))
                     .includingAll(self.getRates(JUDOPSM!DurationType#minute))
    }
    
    // rate of day unit to smaller temporal unit(s) should be valid
    constraint DayRateIsValid {
        guard: self.unitType = JUDOPSM!DurationType#day
        
        check: self.getRates(JUDOPSM!DurationType#millisecond).select(r | r.get('dividend') <> r.get('divisor') * 86400000).size() == 0
           and self.getRates(JUDOPSM!DurationType#second).select(r | r.get('dividend') <> r.get('divisor') * 86400).size() == 0
           and self.getRates(JUDOPSM!DurationType#minute).select(r | r.get('dividend') <> r.get('divisor') * 1440).size() == 0
           and self.getRates(JUDOPSM!DurationType#hour).select(r | r.get('dividend') <> r.get('divisor') * 24).size() == 0
        message: "Rate of day (" + self.name + ") temporal measure unit is invalid: " + 
                 self.getRates(JUDOPSM!DurationType#millisecond)
                     .includingAll(self.getRates(JUDOPSM!DurationType#second))
                     .includingAll(self.getRates(JUDOPSM!DurationType#minute))
                     .includingAll(self.getRates(JUDOPSM!DurationType#hour))
    }
    
    // rate of week unit to smaller temporal unit(s) should be valid
    constraint WeekRateIsValid {
        guard: self.unitType = JUDOPSM!DurationType#week
        
        check: self.getRates(JUDOPSM!DurationType#millisecond).select(r | r.get('dividend') <> r.get('divisor') * 604800000).size() == 0
           and self.getRates(JUDOPSM!DurationType#second).select(r | r.get('dividend') <> r.get('divisor') * 604800).size() == 0
           and self.getRates(JUDOPSM!DurationType#minute).select(r | r.get('dividend') <> r.get('divisor') * 10080).size() == 0
           and self.getRates(JUDOPSM!DurationType#hour).select(r | r.get('dividend') <> r.get('divisor') * 168).size() == 0
           and self.getRates(JUDOPSM!DurationType#day).select(r | r.get('dividend') <> r.get('divisor') * 7).size() == 0
        message: "Rate of week (" + self.name + ") temporal measure unit is invalid: " +
                 self.getRates(JUDOPSM!DurationType#millisecond)
                     .includingAll(self.getRates(JUDOPSM!DurationType#second))
                     .includingAll(self.getRates(JUDOPSM!DurationType#minute))
                     .includingAll(self.getRates(JUDOPSM!DurationType#hour))
                     .includingAll(self.getRates(JUDOPSM!DurationType#day))
    }
    
    // rate of year unit to smaller temporal unit(s) should be valid
    constraint YearRateIsValid {
        guard: self.unitType = JUDOPSM!DurationType#year
        
        check: self.getRates(JUDOPSM!DurationType#month).select(r | r.get('dividend') <> r.get('divisor') * 12).size() == 0
        message: "Rate of year (" + self.name + ") temporal measure unit is invalid: " +
                 self.getRates(JUDOPSM!DurationType#month)
    }
}
