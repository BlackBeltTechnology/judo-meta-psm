import "../../operations/data/_importData.eol";

context JUDOPSM!Endpoint {

    // id is a reserved name    
    constraint IdNameIsNotSupported {
    	check: self.name <> "id"
    	message: "Name 'id' is reserved"
    }
}

context JUDOPSM!Endpoint {
    
    guard: self.partner.isDefined()

    // if partners are specified on both sides of a relation then endpoints must reference to each other
    constraint InvalidPartnerReferences {
    	check: not self.partner.partner.isDefined() or self.partner.partner = self
    	message: "Invalid opposite partner reference for " + self.displayName()
    }
    
    // target and partner types must be aligned
    constraint InvalidPartnerType {
        check: self.partner.getEntityType() = self.reference.target and self.partner.reference.target = self.getEntityType()
        message: "Invalid partner type: " + self.partner.displayName() + " for " + self.displayName()
    }
    
    // 1-1 single required relationship is not supported
	critique OneToOneRelationNotSupported {
    	guard: self.satisfiesAll("InvalidPartnerReferences", "InvalidPartnerType") and self.partner.satisfiesAll("InvalidPartnerReferences", "InvalidPartnerType")
		check: not(self.isRequired() and not self.collection and self.partner.isRequired() and not self.partner.collection)
		message: "1-1 relations required on both sides are not supported: " + self.displayName() + " - " + self.partner.displayName()
	}
}
