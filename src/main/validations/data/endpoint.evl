import "../../operations/data/_importData.eol";

context JUDOPSM!Endpoint {

    // reserved relation names: id
    constraint ReservedRelationName {
    	check: self.name <> "id"
    	message: "Name " + self.displayName() +" is reserved"
    }

	// upper attribute must be at least 1    
    constraint UpperMustBeAtLeastOne {
    	check: self.cardinality.upper > 0 or self.cardinality.upper == -1
    	message: "Invalid upper attribute of " + self.displayName()
    }
    
    // lower property must be less or equal to upper property
    constraint LowerMustBeLessOrEqualToUpper {
        check: self.cardinality.lower <> -1 and (self.cardinality.upper == -1 or self.cardinality.lower <= self.cardinality.upper)
    	message: "Lower (" + self.cardinality.lower + ") must be less or equal to upper (" + self.cardinality.upper + ") of attribute: " + self.displayName()
    }
    
    // partner of an endpoint must be a different one
    constraint SelfPartner {
    	check: not self.partner.isDefined() or self.partner <> self
    	message: "Self partner relation found: " + self.displayName()
    }
}

context JUDOPSM!Endpoint {

    guard: self.partner.isDefined()

    // opposite partner must be defined if a partner if endpoint is already defined
    constraint OppositePartnerIsNotDefined {
    	check: self.partner.partner.isDefined()
    	message: "Missing opposite partner relation for " + self.displayName()
    }

    // if partners are specified on both sides of a relation then endpoints must relation to each other
    constraint InvalidPartnerRelations {
    	guard: self.satisfiesAll("OppositePartnerIsNotDefined")
    	check: self.partner.partner = self
    	message: "Opposite partner relation of " + self.partner.displayName() + " must be " + self.displayName()
    }

    // target and partner types must be aligned
    constraint InvalidPartnerType {
        check: self.partner.getEntityType() = self.target and self.partner.target = self.getEntityType()
        message: "Invalid partner type: " + self.partner.displayName() + " for " + self.displayName()
    }

    // 1-1 single required relationship is not supported
	critique OneToOneRelationNotSupported {
    	guard: self.satisfiesAll("InvalidPartnerRelations", "InvalidPartnerType") and self.partner.satisfiesAll("InvalidPartnerRelations", "InvalidPartnerType")
		check: not(self.isRequired() and not self.isCollection() and self.partner.isRequired() and not self.partner.isCollection())
		message: "1-1 relations required on both sides are not supported: " + self.displayName() + " - " + self.partner.displayName()
	}
}
