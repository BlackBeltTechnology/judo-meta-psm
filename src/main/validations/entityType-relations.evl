context Structure {

	constraint RecursiveStructureIsNotSupported {
		check: not self.hasCircularReference()
		message: "Element " + self.name + " is a recursive struct that is not supported"
	}
}

context Endpoint {
	// endpoint attribute must be a reference
    constraint EndpointAttributeIsNotAReference {
    	check: self.datatype.isTypeOf(Reference)
    	message: "Invalid attribute type of endpoint " + self.displayName()
    }
}

context Endpoint {
    
    guard: self.partner.isDefined()

    // if partners are specified on both sides of a relation then endpoints must reference to each other
    constraint InvalidPartnerReferences {
    	check: not self.partner.partner.isDefined() or self.partner.partner = self
    	message: "Invalid opposite partner reference for " + self.displayName()
    }
    
    // target and partner types must be aligned
    constraint InvalidPartnerType {
        check: self.partner.getEntityType() = self.datatype.target and self.partner.datatype.target = self.getEntityType()
        message: "Invalid partner type: " + self.partner.displayName() + " for " + self.displayName()
    }
    
    // 1-1 single required relationship is not supported
	critique OneToOneRelationNotSupported {
    	guard: self.satisfiesAll("InvalidPartnerReferences", "InvalidPartnerType") and self.partner.satisfiesAll("InvalidPartnerReferences", "InvalidPartnerType")
		check: not(self.required and not self.datatype.collection and self.partner.required and not self.partner.datatype.collection)
		message: "1-1 relations required on both sides are not supported: " + self.displayName() + " - " + self.partner.displayName()
	}
}

@cached
operation Attribute displayName() {
	return self.getEntityType().name + "." + self.name;
}

@cached
operation Attribute getEntityType() {
	return EntityType.all().select(et | et.attributes.select(a | a = self).size() = 1).first();
}

@cached
operation EntityType getAllTypes() {
	var types = Set{self};
	if (self.superTypes.isDefined()) {
		for (st : EntityType in self.superTypes) {
			types.add(st);
		}
	}
	return types;
}

@cached
operation Endpoint getOpposites() {
	var opposites = self.datatype.target.endpoints.select(e | e.datatype.target = self.getEntityType());
	return opposites;
}

@cached
operation Structure hasCircularReference() {
	var subTypes = Set{};
	for (f in self.fields) {
		if (f.datatype.isTypeOf(Structure)) {
			subTypes.add(f.datatype);
		}
	}
	return self.hasCircularReference(subTypes);
}

operation Structure hasCircularReference(visited : Set) {
    if (visited.includes(self)) {
        return true;
    } else if (visited.isEmpty()) {
        return false;
    }

    var subTypes = Set{};
    for (s in visited) {
		for (f in s.fields) {
			if (f.datatype.isTypeOf(Structure)) {
				subTypes.add(f.datatype);
			}
		}
    }

    return self.hasCircularReference(subTypes);
}
