module PsmModelBuilder

import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.mwe2.ecore.*
import hu.blackbelt.emfbuildergenerator.*
import hu.blackbelt.eclipse.emf.genmodel.generator.helper.*

var rootPath = "."

var modelDir = "model"
var javaGenPath = "src-genmodel"
var platformUri = "platform:/resource/hu.blackbelt.judo.meta.psm.model"


Workflow {

	bean = StandaloneSetup {
		platformUri = "${rootPath}"
	}

	// Clean generated sources
	component = DirectoryCleaner {
		directory = "${rootPath}/${javaGenPath}"
	}

	// Generate standard EMF sources from GenModel
	component = EcoreGenerator {
		generateCustomClasses = false
		genModel = "${platformUri}/${modelDir}/psm.genmodel"
		srcPath = "${platformUri}/${javaGenPath}"   
	}

	// Model builder
	component = EMFBuilderGenerator {
		platformUri = "${rootPath}"
		outputPath = "${rootPath}/${javaGenPath}"
		ecoreURI = "${platformUri}/${modelDir}/psm.ecore"
		genModelUri = "${platformUri}/${modelDir}/psm.genmodel"
	}

	// Helper for model
	bean = engine.ModelHelperGeneratorStandaloneSetup : genModelHelperSetup {
		config = {
			javaGenPath = "${rootPath}/${javaGenPath}"
			}
		doInit = true
	}

	component = org.eclipse.xtext.mwe.Reader {
		path = "${rootPath}/${modelDir}"
		register = genModelHelperSetup
		loadResource = {
			slot = "model"
		}
	}
	
	component = org.eclipse.xtext.generator.GeneratorComponent {
		register = genModelHelperSetup
        	slot = 'model'
        	outlet = {
          	  path = "${rootPath}/${javaGenPath}"
        	}
   	}	

     // Runtime model for Judo
	bean = hu.blackbelt.judo.eclipse.emf.genmodel.generator.runtimemodel.engine.RuntimeModelGeneratorStandaloneSetup : genRuntimeModelSetup {
		config = {
			javaGenPath = "${rootPath}/${javaGenPath}"
		}
		doInit = true
	}

	component = org.eclipse.xtext.mwe.Reader {
		path = "${rootPath}/${modelDir}"
		register = genRuntimeModelSetup
		loadResource = {
			slot = "model"
		}
	}
	
	component = org.eclipse.xtext.generator.GeneratorComponent {
		register = genRuntimeModelSetup
        	slot = 'model'
        	outlet = {
       		path = "${rootPath}/${javaGenPath}"
        	}
	}
}
