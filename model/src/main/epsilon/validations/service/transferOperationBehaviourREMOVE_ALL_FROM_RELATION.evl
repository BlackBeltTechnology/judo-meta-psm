context PSM!TransferOperationBehaviour {

	guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#REMOVE_ALL_FROM_RELATION
	
	constraint OperationOfRemoveAllFromRelationBehaviourIsValid {
		check: self.eContainer.isKindOf(PSM!UnboundOperation)
		message: "'REMOVE_ALL_FROM_RELATION' behaviour: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ") must be owned by an unbound operation"
	}
	
	constraint OwnerOfRemoveAllFromRelationBehaviourIsValid {
		check: self.owner.isDefined() and self.owner.isKindOf(PSM!ExposedGraph) and self.owner.mappedTransferObjectType == self.eContainer.eContainer
		message: "Owner of 'REMOVE_ALL_FROM_RELATION' behaviour: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ") must be an exposed graph"
	}
	
	constraint RelationOfRemoveAllFromRelationBehaviourIsValid {
		check: self.relation.isDefined() and psmUtils.getAllTransferObjectRelations(self.owner.mappedTransferObjectType).includes(self.relation)
		message: "Relation must be defined for 'REMOVE_ALL_FROM_RELATION' behaviour: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ")"
	}
	
	constraint RemoveAllFromRelationOperationInputParameterIsDefined {
    	guard: self.satisfiesAll("OwnerOfRemoveAllFromRelationBehaviourIsValid","OwnerOfRemoveAllFromRelationBehaviourIsValid","RelationOfRemoveAllFromRelationBehaviourIsValid")
    	check: self.eContainer.input.isDefined()
    	message: "'REMOVE_ALL_FROM_RELATION' operation must have an input parameter named 'input'"
    }
    
    constraint RemoveAllFromRelationOperationInputNameIsValid {
    	guard: self.satisfiesAll("RemoveAllFromRelationOperationInputParameterIsDefined","OwnerOfRemoveAllFromRelationBehaviourIsValid","OwnerOfRemoveAllFromRelationBehaviourIsValid","RelationOfRemoveAllFromRelationBehaviourIsValid")
    	check: self.eContainer.input.name == "input"
    	message: "'REMOVE_ALL_FROM_RELATION' operation's input parameter must be named 'input'"
    }
    
    constraint RemoveAllFromRelationOperationInputTypeIsValid {
    	guard: self.satisfiesAll("RemoveAllFromRelationOperationInputParameterIsDefined","OwnerOfRemoveAllFromRelationBehaviourIsValid","OwnerOfRemoveAllFromRelationBehaviourIsValid","RelationOfRemoveAllFromRelationBehaviourIsValid")
    	check: self.eContainer.input.type.getAllSuperTransferObjectTypes().including(self.eContainer.input.type).includes(self.owner.mappedTransferObjectType)
    	message: "Type of 'REMOVE_ALL_FROM_RELATION' operation's input parameter must be kind of referenced mapped transfer object type"
    }
    
    constraint RemoveAllFromRelationOperationInputCardinalityIsValid {
    	guard: self.satisfiesAll("RemoveAllFromRelationOperationInputParameterIsDefined","OwnerOfRemoveAllFromRelationBehaviourIsValid","OwnerOfRemoveAllFromRelationBehaviourIsValid","RelationOfRemoveAllFromRelationBehaviourIsValid")
    	check: self.eContainer.input.cardinality.lower == 1 and
    		self.eContainer.input.cardinality.upper == 1 
    	message: "Cardinality of 'REMOVE_ALL_FROM_RELATION' operation's input parameter must be 1..1"
    }
    
    constraint RemoveAllFromRelationOperationOutputParameterIsNotDefined {
    	guard: self.satisfiesAll("OwnerOfRemoveAllFromRelationBehaviourIsValid","OwnerOfRemoveAllFromRelationBehaviourIsValid","RelationOfRemoveAllFromRelationBehaviourIsValid")
    	check: self.eContainer.output.isUndefined()
    	message: "'REMOVE_ALL_FROM_RELATION' operation cannot have an output parameter"
    }
}
	