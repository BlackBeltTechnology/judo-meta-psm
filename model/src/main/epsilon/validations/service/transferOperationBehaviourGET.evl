context PSM!TransferOperationBehaviour {
	
	guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#GET
	
	constraint OperationOfGetBehaviourIsValid {
		check: self.eContainer.isKindOf(PSM!UnboundOperation)
		message: "'GET' operation: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ") must be unbound operation"
	}
	
	constraint OwnerOfGetBehaviourIsExposedGraph {
		check: self.owner.isDefined() and self.owner.isKindOf(PSM!ExposedGraph)
		message: "Owner of 'GET' operation: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ") must be exposed graph."
	}
	
	constraint OwnerOfGetBehaviourIsValid {
		guard: self.satisfies("OwnerOfGetBehaviourIsExposedGraph") 
		check: self.owner.mappedTransferObjectType == self.eContainer.eContainer
		message: "Mapped transfer object of owner exposed graph of 'GET' operation: " + self.eContainer.name  + 
			" (in: " + self.eContainer.eContainer.name + ") must match the operation's container."
	}
	
	constraint RelationOfGetBehaviourIsUndefined {
		check: self.relation.isUndefined()
		message: "Relation must be undefined for 'GET' operation: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ")"
	}
	
	constraint GetOperationInputParameterIsNotDefined {
    	guard: self.satisfiesAll("OperationOfGetBehaviourIsValid", "OwnerOfGetBehaviourIsValid", "RelationOfGetBehaviourIsUndefined")
    	check: self.eContainer.input.isUndefined()
    	message: "'GET' operation cannot have an input parameter (operation: " + self.eContainer.name + ")"
    }
    
    constraint GetOperationOutputParameterIsDefined {
    	guard: self.satisfiesAll("OperationOfGetBehaviourIsValid", "OwnerOfGetBehaviourIsValid", "RelationOfGetBehaviourIsUndefined")
    	check: self.eContainer.output.isDefined()
    	message: "'GET' operation must have an output parameter named 'output' (operation: " + self.eContainer.name + ")"
    }
    
    constraint GetOperationOutputNameIsValid {
    	guard: self.satisfiesAll("GetOperationOutputParameterIsDefined", "OperationOfGetBehaviourIsValid", "OwnerOfGetBehaviourIsValid", "RelationOfGetBehaviourIsUndefined")
    	check: self.eContainer.output.name == "output"
    	message: "'GET' operation's output parameter must be named 'output' (operation: " + self.eContainer.name + ")"
    }
    
    constraint GetOperationOutputTypeIsValid {
    	guard: self.satisfiesAll("GetOperationOutputParameterIsDefined", "OperationOfGetBehaviourIsValid", "OwnerOfGetBehaviourIsValid", "RelationOfGetBehaviourIsUndefined")
    	check: self.eContainer.output.type.getAllSuperTransferObjectTypes().including(self.eContainer.output.type).includes(self.owner.mappedTransferObjectType)
    	message: "Type of 'GET' operation's output parameter must be kind of exposed graph's mapped transfer object type (operation: " + self.eContainer.name + ")"
    }
    
    constraint GetOperationOutputCardinalityIsValid {
    	guard: self.satisfiesAll("GetOperationOutputParameterIsDefined", "OperationOfGetBehaviourIsValid", "OwnerOfGetBehaviourIsValid", "RelationOfGetBehaviourIsUndefined")
    	check: self.eContainer.output.cardinality.lower == self.owner.cardinality.lower and
    		self.eContainer.output.cardinality.upper == self.owner.cardinality.upper 
    	message: "Cardinality of 'GET' operation's output parameter must be the same as its owner's (operation: " + self.eContainer.name + ")"
    }
}
