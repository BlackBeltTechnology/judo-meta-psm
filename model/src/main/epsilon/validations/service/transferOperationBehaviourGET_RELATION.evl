context PSM!TransferOperationBehaviour {
	
	guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#GET_RELATION
	
	constraint OperationOfGetRelationBehaviourIsValid {
		check: self.eContainer.isKindOf(PSM!BoundTransferOperation) 
		message: "'GET_RELATION' behaviour (" + self.eContainer.eContainer.name + "." + self.eContainer.name + ") must be owned by a bound transfer operation"
	}
	
	constraint OwnerOfGetRelationBehaviourIsValid {
		check: self.owner.isDefined() and self.owner.isKindOf(PSM!TransferObjectRelation)
			and psmUtils.getAllTransferObjectRelations(self.eContainer.eContainer).includes(self.owner)
		message: "Owner of 'GET_RELATION' behaviour  (" + self.eContainer.eContainer.name + "." + self.eContainer.name + ") must be a transfer object relation inherited or owned by the transfer object type containing the operation"
	}
	
	constraint RelationOfGetRelationBehaviourIsUndefined {
		check: self.relation.isUndefined()
		message: "Relation must be undefined for 'GET_RELATION' behaviour"
	}
	
	constraint GetRelationOperationInputParameterIsNotDefined {
    	guard: self.satisfiesAll("OperationOfGetRelationBehaviourIsValid","OwnerOfGetRelationBehaviourIsValid","RelationOfGetRelationBehaviourIsUndefined")
    		and self.eContainer.satisfies("BindingIsValid")
    	check: self.eContainer.binding.input.isUndefined()
    	message: "'GET_RELATION' operation (" + self.eContainer.eContainer.name + "." + self.eContainer.name + ") cannot have an input parameter"
    }
    
    constraint GetRelationOperationOutputParameterIsDefined {
    	guard: self.satisfiesAll("OperationOfGetRelationBehaviourIsValid","OwnerOfGetRelationBehaviourIsValid","RelationOfGetRelationBehaviourIsUndefined")
    		and self.eContainer.satisfies("BindingIsValid")
    	check: self.eContainer.binding.output.isDefined()
    	message: "'GET_RELATION' operation (" + self.eContainer.eContainer.name + "." + self.eContainer.name + ") must have an output parameter named 'output'"
    }
    
    constraint GetRelationOperationOutputNameIsValid {
    	guard: self.satisfiesAll("GetRelationOperationOutputParameterIsDefined","OperationOfGetRelationBehaviourIsValid","OwnerOfGetRelationBehaviourIsValid","RelationOfGetRelationBehaviourIsUndefined")
    		and self.eContainer.satisfies("BindingIsValid")
    	check: self.eContainer.binding.output.name == "output"
    	message: "'GET_RELATION' operation's (" + self.eContainer.eContainer.name + "." + self.eContainer.name + ") output parameter must be named 'output'"
    }
    
    constraint GetRelationOperationOutputTypeIsValid {
    	guard: self.satisfiesAll("GetRelationOperationOutputParameterIsDefined","OperationOfGetRelationBehaviourIsValid","OwnerOfGetRelationBehaviourIsValid","RelationOfGetRelationBehaviourIsUndefined")
    		and self.eContainer.satisfies("BindingIsValid")
    	check: self.eContainer.binding.output.type.entityType.getAllSuperEntityTypes().including(self.eContainer.binding.output.type.entityType).includes(self.owner.target.entityType)
    	message: "Type of 'GET_RELATION' operation's (" + self.eContainer.eContainer.name + "." + self.eContainer.name + ") output parameter must be kind of referenced mapped transfer object type"
    }
    
    constraint GetRelationOperationOutputCardinalityIsValid {
    	guard: self.satisfiesAll("GetRelationOperationOutputParameterIsDefined","OperationOfGetRelationBehaviourIsValid","OwnerOfGetRelationBehaviourIsValid","RelationOfGetRelationBehaviourIsUndefined")
    		and self.eContainer.satisfies("BindingIsValid")
    	check: self.eContainer.binding.output.cardinality.lower == self.owner.cardinality.lower and
    		self.eContainer.binding.output.cardinality.upper == self.owner.cardinality.upper 
    	message: "Cardinality of 'GET_RELATION' operation's (" + self.eContainer.eContainer.name + "." + self.eContainer.name + ") output parameter must be the same as its owner's"
    }
}
