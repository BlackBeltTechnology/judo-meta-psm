context PSM!TransferOperationBehaviour {
	
	guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#ADD_ALL_TO_RELATION
	
	constraint OperationOfAddAllToRelationBehaviourIsValid {
		check: self.eContainer.isKindOf(PSM!UnboundOperation)
		message: "'ADD_ALL_TO_RELATION' operation: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ") must be owned by an unbound operation"
	}
	
	constraint OwnerOfAddAllToRelationBehaviourIsExposedGraph {
		check: self.owner.isDefined() and self.owner.isKindOf(PSM!ExposedGraph)
		message: "Owner of 'ADD_ALL_TO_RELATION' operation: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ") must be exposed graph."
	}
	
	constraint OwnerOfAddAllToRelationBehaviourIsValid {
		guard: self.satisfies("OwnerOfAddAllToRelationBehaviourIsExposedGraph") 
		check: self.owner.mappedTransferObjectType == self.eContainer.eContainer
		message: "Mapped transfer object of owner exposed graph of 'ADD_ALL_TO_RELATION' operation: " + self.eContainer.name  + 
			" (in: " + self.eContainer.eContainer.name + ") must match the operation's container."
	}
	
	constraint RelationOfAddAllToRelationBehaviourIsDefined {
		guard: self.satisfies("OwnerOfAddAllToRelationBehaviourIsValid")
		check: self.relation.isDefined() 
		message: "Relation must be defined for 'ADD_ALL_TO_RELATION' operation: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ")"
	}
	
	constraint RelationOfAddAllToRelationBehaviourIsValid {
		guard: self.satisfiesAll("OwnerOfAddAllToRelationBehaviourIsValid","RelationOfAddAllToRelationBehaviourIsDefined")
		check: psmUtils.getAllTransferObjectRelations(self.owner.mappedTransferObjectType).includes(self.relation)
		message: "Relation of 'ADD_ALL_TO_RELATION' operation: " + self.eContainer.name  + " must be one of the transfer object type referenced by the operation's owner"
	}
	
	constraint AddAllToRelationOperationInputParameterIsDefined {
    	guard: self.satisfiesAll("OperationOfAddAllToRelationBehaviourIsValid","OwnerOfAddAllToRelationBehaviourIsValid","RelationOfAddAllToRelationBehaviourIsValid")
    	check: self.eContainer.input.isDefined()
    	message: "'ADD_ALL_TO_RELATION' operation must have an input parameter named 'input' (operation: " + self.eContainer.name + ")"
    }
    
    constraint AddAllToRelationOperationInputNameIsValid {
    	guard: self.satisfiesAll("AddAllToRelationOperationInputParameterIsDefined","OperationOfAddAllToRelationBehaviourIsValid","OwnerOfAddAllToRelationBehaviourIsValid","RelationOfAddAllToRelationBehaviourIsValid")
    	check: self.eContainer.input.name == "input"
    	message: "'ADD_ALL_TO_RELATION' operation's input parameter must be named 'input' (operation: " + self.eContainer.name + ")"
    }
    
    constraint AddAllToRelationOperationInputTypeIsValid {
    	guard: self.satisfiesAll("AddAllToRelationOperationInputParameterIsDefined","OperationOfAddAllToRelationBehaviourIsValid","OwnerOfAddAllToRelationBehaviourIsValid","RelationOfAddAllToRelationBehaviourIsValid")
    	check: self.eContainer.input.type.getAllSuperTransferObjectTypes().including(self.eContainer.input.type).includes(self.owner.mappedTransferObjectType)
    	message: "Type of 'ADD_ALL_TO_RELATION' operation's input parameter must be kind of referenced mapped transfer object type (operation: " + self.eContainer.name + ")"
    }
    
    constraint AddAllToRelationOperationInputCardinalityIsValid {
    	guard: self.satisfiesAll("AddAllToRelationOperationInputParameterIsDefined","OperationOfAddAllToRelationBehaviourIsValid","OwnerOfAddAllToRelationBehaviourIsValid","RelationOfAddAllToRelationBehaviourIsValid")
    	check: self.eContainer.input.cardinality.lower == 1 and
    		self.eContainer.input.cardinality.upper == 1 
    	message: "Cardinality of 'ADD_ALL_TO_RELATION' operation's input parameter must be 1..1 (operation: " + self.eContainer.name + ")"
    }
    
    constraint AddAllToRelationOperationOutputParameterIsNotDefined {
    	guard: self.satisfiesAll("OperationOfAddAllToRelationBehaviourIsValid","OwnerOfAddAllToRelationBehaviourIsValid","RelationOfAddAllToRelationBehaviourIsValid")
    	check: self.eContainer.output.isUndefined()
    	message: "'ADD_ALL_TO_RELATION' operation cannot have an output parameter (operation: " + self.eContainer.name + ")"
    }
}
	