context PSM!TransferOperationBehaviour {
	
	constraint OperationOfAddAllToRelationBehaviourIsValid {
		guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#ADD_ALL_TO_RELATION
		check: self.eContainer.isKindOf(PSM!UnboundOperation)
		message: "'ADD_ALL_TO_RELATION' behaviour: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ") must be owned by an unbound operation"
	}
	
	constraint OwnerOfAddAllToRelationBehaviourIsValid {
		guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#ADD_ALL_TO_RELATION
		check: self.owner.isDefined() and self.owner.isKindOf(PSM!ExposedGraph) and self.owner.mappedTransferObjectType == self.eContainer.eContainer
		message: "Owner of 'ADD_ALL_TO_RELATION' behaviour: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ") must be an exposed graph"
	}
	
	constraint RelationOfAddAllToRelationBehaviourIsValid {
		guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#ADD_ALL_TO_RELATION
		check: self.relation.isDefined() and self.relation.eContainer == self.owner.mappedTransferObjectType
		message: "Relation must be defined for 'ADD_ALL_TO_RELATION' behaviour: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ")"
	}
	
	constraint AddAllToRelationOperationInputParameterIsDefined {
    	guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#ADD_ALL_TO_RELATION and
    		self.satisfiesAll("OperationOfAddAllToRelationBehaviourIsValid","OwnerOfAddAllToRelationBehaviourIsValid","RelationOfAddAllToRelationBehaviourIsValid")
    	check: self.eContainer.input.isDefined()
    	message: "'ADD_ALL_TO_RELATION' operation must have an input parameter named 'input'"
    }
    
    constraint AddAllToRelationOperationInputNameIsValid {
    	guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#ADD_ALL_TO_RELATION and
    		self.satisfiesAll("AddAllToRelationOperationInputParameterIsDefined","OperationOfAddAllToRelationBehaviourIsValid","OwnerOfAddAllToRelationBehaviourIsValid","RelationOfAddAllToRelationBehaviourIsValid")
    	check: self.eContainer.input.name == "input"
    	message: "'ADD_ALL_TO_RELATION' operation's input parameter must be named 'input'"
    }
    
    constraint AddAllToRelationOperationInputTypeIsValid {
    	guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#ADD_ALL_TO_RELATION and
    		self.satisfiesAll("AddAllToRelationOperationInputParameterIsDefined","OperationOfAddAllToRelationBehaviourIsValid","OwnerOfAddAllToRelationBehaviourIsValid","RelationOfAddAllToRelationBehaviourIsValid")
    	check: self.eContainer.input.type.entityType.getAllSuperEntityTypes().including(self.eContainer.input.type.entityType).includes(self.owner.mappedTransferObjectType.entityType)
    	message: "Type of 'ADD_ALL_TO_RELATION' operation's input parameter must be kind of referenced mapped transfer object type"
    }
    
    constraint AddAllToRelationOperationInputCardinalityIsValid {
    	guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#ADD_ALL_TO_RELATION and
    		self.satisfiesAll("AddAllToRelationOperationInputParameterIsDefined","OperationOfAddAllToRelationBehaviourIsValid","OwnerOfAddAllToRelationBehaviourIsValid","RelationOfAddAllToRelationBehaviourIsValid")
    	check: self.eContainer.input.cardinality.lower == 1 and
    		self.eContainer.input.cardinality.upper == 1 
    	message: "Cardinality of 'ADD_ALL_TO_RELATION' operation's input parameter must be 1..1"
    }
    
    constraint AddAllToRelationOperationOutputParameterIsNotDefined {
    	guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#ADD_ALL_TO_RELATION and
    		self.satisfiesAll("OperationOfAddAllToRelationBehaviourIsValid","OwnerOfAddAllToRelationBehaviourIsValid","RelationOfAddAllToRelationBehaviourIsValid")
    	check: self.eContainer.output.isUndefined()
    	message: "'ADD_ALL_TO_RELATION' operation cannot have an output parameter"
    }
}
	