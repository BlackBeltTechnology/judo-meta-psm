context PSM!TransferOperationBehaviour {
	
	guard: self.eContainer.isDefined() and self.behaviourType == PSM!TransferOperationBehaviourType#UNSET_RELATION
	
	constraint OperationOfUnsetRelationBehaviourIsValid {
		check: self.eContainer.isKindOf(PSM!UnboundOperation)
		message: "'UNSET_RELATION' operation: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ") must be owned by an unbound operation"
	}
	
	constraint OwnerOfUnsetRelationBehaviourIsExposedGraph {
		check: self.owner.isDefined() and self.owner.isKindOf(PSM!ExposedGraph)
		message: "Owner of 'UNSET_RELATION' operation: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ") must be exposed graph."
	}
	
	constraint OwnerOfUnsetRelationBehaviourIsValid {
		guard: self.satisfies("OwnerOfUnsetRelationBehaviourIsExposedGraph") 
		check: self.owner.mappedTransferObjectType == self.eContainer.eContainer
		message: "Mapped transfer object of owner exposed graph of 'UNSET_RELATION' operation: " + self.eContainer.name  + 
			" (in: " + self.eContainer.eContainer.name + ") must match the operation's container."
	}
	
	constraint RelationOfUnsetRelationBehaviourIsDefined {
		guard: self.satisfies("OwnerOfUnsetRelationBehaviourIsValid")
		check: self.relation.isDefined() 
		message: "Relation must be defined for 'UNSET_RELATION' operation: " + self.eContainer.name  + " (in: " + self.eContainer.eContainer.name + ")"
	}
	
	constraint RelationOfUnsetRelationBehaviourIsValid {
		guard: self.satisfiesAll("OwnerOfUnsetRelationBehaviourIsValid","RelationOfUnsetRelationBehaviourIsDefined")
		check: psmUtils.getAllTransferObjectRelations(self.owner.mappedTransferObjectType).includes(self.relation)
		message: "Relation of 'UNSET_RELATION' operation: " + self.eContainer.name  + " must be one of the transfer object type referenced by the operation's owner"
	}
	
	constraint UnsetRelationOperationInputParameterIsDefined {
    	guard: self.satisfiesAll("OperationOfUnsetRelationBehaviourIsValid","OwnerOfUnsetRelationBehaviourIsValid","RelationOfUnsetRelationBehaviourIsValid")
    	check: self.eContainer.input.isDefined()
    	message: "'UNSET_RELATION' operation must have an input parameter named 'input' (operation: " + self.eContainer.name + ")"
    }
    
    constraint UnsetRelationOperationInputNameIsValid {
    	guard: self.satisfiesAll("UnsetRelationOperationInputParameterIsDefined","OperationOfUnsetRelationBehaviourIsValid","OwnerOfUnsetRelationBehaviourIsValid","RelationOfUnsetRelationBehaviourIsValid")
    	check: self.eContainer.input.name == "input"
    	message: "'UNSET_RELATION' operation's input parameter must be named 'input' (operation: " + self.eContainer.name + ")"
    }
    
    constraint UnsetRelationOperationInputTypeIsValid {
    	guard: self.satisfiesAll("UnsetRelationOperationInputParameterIsDefined","OperationOfUnsetRelationBehaviourIsValid","OwnerOfUnsetRelationBehaviourIsValid","RelationOfUnsetRelationBehaviourIsValid")
    	check: self.eContainer.input.type.getAllSuperTransferObjectTypes().including(self.eContainer.input.type).includes(self.owner.mappedTransferObjectType)
    	message: "Type of 'UNSET_RELATION' operation's input parameter must be kind of referenced mapped transfer object type (operation: " + self.eContainer.name + ")"
    }
    
    constraint UnsetRelationOperationInputCardinalityIsValid {
    	guard: self.satisfiesAll("UnsetRelationOperationInputParameterIsDefined","OperationOfUnsetRelationBehaviourIsValid","OwnerOfUnsetRelationBehaviourIsValid","RelationOfUnsetRelationBehaviourIsValid")
    	check: self.eContainer.input.cardinality.lower == 1 and
    		self.eContainer.input.cardinality.upper == 1 
    	message: "Cardinality of 'UNSET_RELATION' operation's input parameter must be 1..1 (operation: " + self.eContainer.name + ")"
    }
    
    constraint UnsetRelationOperationOutputParameterIsNotDefined {
    	guard: self.satisfiesAll("OperationOfUnsetRelationBehaviourIsValid","OwnerOfUnsetRelationBehaviourIsValid","RelationOfUnsetRelationBehaviourIsValid")
    	check: self.eContainer.output.isUndefined()
    	message: "'UNSET_RELATION' operation cannot have an output parameter (operation: " + self.eContainer.name + ")"
    }
}
	