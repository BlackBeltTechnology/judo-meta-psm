context PSM!Primitive {
    // primitive type names should be unique
    critique PrimitiveTypeNamesAreUnique {
        check: not PSM!Primitive.all.excluding(self).selectOne(t | t.name.toLowerCase() == self.name.toLowerCase()).isDefined()
        message: "Primitive type name is not unique: " + self.name
    }
}

context PSM!EntityType {
    // entity type names should be unique
    critique EntityTypeNamesAreUnique {
        check: not PSM!EntityType.all.excluding(self).selectOne(t | t.name.toLowerCase() == self.name.toLowerCase()).isDefined()
        message: "Entity type name is not unique: " + self.name
    }
}

context PSM!TransferObjectType {
    // transfer object type names should be unique
    critique TransferObjectTypeNamesAreUnique {
        check: not PSM!TransferObjectType.all.excluding(self).selectOne(t | t.name.toLowerCase() == self.name.toLowerCase()).isDefined()
        message: "Transfer object type name is not unique: " + self.name
    }
}

context PSM!TransferObjectRelation {

    constraint TransferObjectRelationIsEmbedded {
		guard: self.target.isKindOf(PSM!UnmappedTransferObjectType)
		check: self.embedded
		message: "Transfer object relation " + self.name + " is referencing to unembedded unmapped transfer object type: " + self.target.name
	}
	
	constraint TransferObjectRelationBindingIsValid {
		guard: self.eContainer().isDefined() and self.eContainer.isKindOf(MappedTransferObjectType) and self.binding.isDefined()
		check: 	self.eContainer().entityType.getAllSuperEntityTypes().including(self.eContainer().entityType).contains(self.binding.eContainer()) or
				self.binding.isKindOf(PSM!StaticNavigation)	
		message: "Binding of transfer object relation " + self.name + " of mapped transfer object " + self.eContainer().name +
				" must either match the entity type of the mapped tranfer object or be StaticNavigation."
	}	
}

context PSM!TransferAttribute {
	
	constraint TransferAttributeBindingIsValid {
		guard: self.eContainer().isDefined() and self.eContainer.isKindOf(MappedTransferObjectType) and self.binding.isDefined()
		check: self.eContainer().entityType.getAllSuperEntityTypes().including(self.eContainer().entityType).contains(self.binding.eContainer()) or
				self.binding.isKindOf(PSM!StaticData)
		message: "Binding of transfer attribute " + self.name + " of mapped transfer object " + self.eContainer().name +
				" must either match the entity type of the mapped tranfer object or be StaticData."
	}
}

