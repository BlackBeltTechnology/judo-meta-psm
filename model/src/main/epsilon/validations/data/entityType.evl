context PSM!EntityType {

    // entity type names should be unique
    critique EntityTypeNamesAreUnique {
        check: not PSM!EntityType.all.excluding(self).selectOne(t | t.name.toLowerCase() == self.name.toLowerCase()).isDefined()
        message: "Entity type name is not unique: " + self.name
    }

    // recursive inheritance is not supported
	constraint InheritanceIsNotRecursive {
		check: not self.getAllSuperEntityTypes().contains(self)
		message: "Entity type " + self.name + " is recursive"
	}

    // entity type is inheriting unique named element names
    constraint InheritingNamedElementsOfTheSameNameIsNotAllowed {
        check: psmUtils.getInheritedNamedElementNames(self)
                    .forAll(n | self.getAllSuperEntityTypes()
                                    .collect(s | s.eContents()).flatten()
                                    .select(c | c.isKindOf(PSM!NamedElement))
                                    .select(e | e.name.toLowerCase() == n.toLowerCase()).size() == 1 )
        message: "Entity type: " + self.name + " has inherited named elements of the same name."
    }

    //TODO-suspended: Seq & Attr, Seq & Rel, Se & RelCC (duplicate name check) [after Sequence becomes supported]
}