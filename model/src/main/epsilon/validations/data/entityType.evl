context PSM!EntityType {

    // recursive inheritance is not supported
	constraint InheritanceIsNotRecursive {
		check: not self.getAllSuperEntityTypes().contains(self)
		message: "Entity type " + self.name + " is recursive"
	}

	// entity has no multiple attributes with the same name
	constraint AttributeNameIsUnique {
		check: self.attributes.select(a | self.attributes.excluding(a).select(a2 | a2.name == a.name).size() > 0).size() == 0
		message: "Multiple attributes are added to entity " + self.name + " with the same name"
	}

	// entity has nomultiple relations with the same name
	constraint RelationNameIsUnique {
		check: self.relations.select(e | self.relations.excluding(e).select(e2 | e2.name == e.name).size() > 0).size() == 0
		message: "Multiple relations are added to entity " + self.name + " with the same name"
	}

	// entity has no attribute(s) and relations(s) with the same name
	constraint NoAttributeAndRelationAreWithTheSameName {
		check: self.relations.select(e | self.attributes.select(a | e.name == a.name).size() > 0).size() == 0
		message: "Entity " + self.name + " has attributes and relations with the same name"
	}

	//TODO-suspended: junit test [after RelCC becomes supported]
	// name of relation count constraints must be unique
    constraint RelationCountConstraintHasUniqueName {
        check: self.relationCountConstraints.select(rcc1 | self.relationCountConstraints.excluding(rcc1).select(rcc2 | rcc2.name = rcc1.name).size() > 0).size() = 0
        message: "Relation count constraints are not unique: " + self.name
    }

    //TODO-suspended: junit test [after RelCC becomes supported]
    constraint NoAttributeAndRelationCountConstraintWithTheSameName {
        check: self.attributes.select(e | self.relationCountConstraints.select(a | e.name == a.name).size() > 0).size() == 0
        message: "Entity " + self.name + " has attributes and relation count constraints with the same name"
    }

    //TODO-suspended: junit test [after RelCC becomes supported]
    constraint NoRelationAndRelationCountConstraintWithTheSameName {
        check: self.relations.select(e | self.relationCountConstraints.select(a | e.name == a.name).size() > 0).size() == 0
        message: "Entity " + self.name + " has relations and relation count constraints with the same name"
    }

    //TODO-suspended: Sequence (duplicate name check) [after Sequence becomes supported]

    //TODO-suspended: Seq & Attr, Seq & Rel, Se & RelCC (duplicate name check) [after Sequence becomes supported]

	//TODO-suspended: remove constraint below [after RelCC becomes supported]
	// relation count constraints are not supported yet
    constraint RelationCountConstraintsAreNotSupportedYet {
        check: self.relationCountConstraints.size() == 0
        message: "Relation count constraints are not supported yet"
    }
}

context PSM!Attribute {
    // attributes must be attached to entities
    constraint AttributeBelongsToEntity {
        check: PSM!EntityType.all.select(et | et.attributes.contains(self)).size() == 1
        message: "Orphan attribute: " + self.name
    }
}
