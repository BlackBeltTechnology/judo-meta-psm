context Endpoint {

    // reserved relation names: id
    constraint RelationNameIsNotReserved {
    	check: self.name <> "id"
    	message: "Name " + self +" is reserved"
    }

	// upper attribute must be at least 1    
    constraint UpperIsAtLeastOne {
    	check: self.cardinality.upper > 0 or self.cardinality.upper == -1
    	message: "Invalid upper attribute of " + self
    }
    
    // lower property must be less or equal to upper property
    constraint LowerMustBeLessOrEqualToUpper {
        check: self.cardinality.lower <> -1 and (self.cardinality.upper == -1 or self.cardinality.lower <= self.cardinality.upper)
    	message: "Lower (" + self.cardinality.lower + ") must be less or equal to upper (" + self.cardinality.upper + ") of attribute: " + self
    }
    
    // partner of an endpoint must be a different one
    constraint PartnerIsNotSelf {
    	check: not self.partner.isDefined() or self.partner <> self
    	message: "Self partner relation found: " + self
    }
}

context Endpoint {

    guard: self.partner.isDefined()

    // opposite partner must be defined if a partner if endpoint is already defined
    constraint OppositePartnerIsDefined {
    	check: self.partner.partner.isDefined()
    	message: "Missing opposite partner relation for " + self
    }

    // if partners are specified on both sides of a relation then endpoints must relation to each other
    constraint ValidPartnerRelations {
    	guard: self.satisfiesAll("OppositePartnerIsDefined")
    	check: self.partner.partner = self
    	message: "Opposite partner relation of " + self.partner + " must be " + self
    }

    // target and partner types must be aligned
    constraint ValidPartnerType {
        check: self.partner.getEntityType() = self.target and self.partner.target = self.getEntityType()
        message: "Invalid partner type: " + self.partner + " for " + self
    }

    // 1-1 single required relationship is not supported
	critique OneToOneRelationsAreNotRecommended {
    	guard: self.satisfiesAll("ValidPartnerRelations", "ValidPartnerType") and self.partner.satisfiesAll("ValidPartnerRelations", "ValidPartnerType")
		check: not(self.isRequired() and not self.isCollection() and self.partner.isRequired() and not self.partner.isCollection())
		message: "1-1 relations required on both sides are not recommended: " + self + " - " + self.partner
	}
}
