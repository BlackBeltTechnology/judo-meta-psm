//TODO-endcheck
context PSM!NamespaceElement {
    // namespace element must have exactly one namespace
    constraint NamespaceElementBelongsToOneNamespace {
        check: PSM!Namespace.all.select(n | n.elements.contains(self)).size() == 1
        message: "Element " + self.name + " must belong to exactly 1 namespace"
    }

    constraint ElementNameContainsAtLeastOneCharacter {
        check: self.name.length() > 0
        message: "NamespaceElement name must contain at least one character. Zero length name found in " + self
    }

    constraint ElementNameContainsValidCharacters {
        guard: self.satisfies("ElementNameContainsAtLeastOneCharacter")
        check: self.name.matches("([a-z]|[A-Z]|[0-9]|_)+")
        message: "NamespaceElement's name can only contain english letters (A-Z, a-z), digits (0-9) and underscore characters (_): " + self.name
    }

    constraint ElementNameCannotContainSubsequentUnderscores {
        guard: self.satisfies("ElementNameContainsAtLeastOneCharacter")

        check: not "__".isSubstringOf(self.name)
        message: "NamespaceElement's name cannot contain two subsequent underscore characters: " + self.name
    }

    constraint ElementNameFirstCharacterCannotBeDigit {
        guard: self.satisfies("ElementNameContainsAtLeastOneCharacter")

        check: self.name.characterAt(0).matches("[a-z]|[A-Z]|_")
        message: "NamespaceElement's name must start with an english letter or an underscore: " + self.name
    }

    constraint ElementNameLastCharacterCannotBeUnderscore {
        guard: self.satisfies("ElementNameContainsAtLeastOneCharacter")

        check: not self.name.endsWith("_")
        message: "NamespaceElement's name must end with an english letter or a digit: " + self.name
    }
}
